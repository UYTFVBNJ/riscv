`timescale 1 ps/ 1 ps
`include "../src/common.v"

`define TESTBENCH_PWD "E:/test/riscv/fpga/testbench/"

module riscv_test();

	reg clk = 1'b0;
	reg rst = 1'b0;
	
	
	reg [`DATA_BUS] ram[128*1024-1:0];
	wire ram_mem_rw;
	wire [`DATA_BUS] ram_if_addr;
	wire [`DATA_BUS] ram_mem_addr, ram_mem_wdata;
	reg  [`DATA_BUS] ram_if_data, ram_mem_rdata;

	always @(posedge clk) begin
		ram_if_data <= ram[ram_if_addr[31:2]];
		if (ram_mem_rw == `MEM_READ)
			ram_mem_rdata <= ram[ram_mem_addr[31:2]];
		else
			ram[ram_mem_addr[31:2]] <= ram_mem_wdata;
	end
	
	riscv_core core(
		clk, rst,
		ram_if_addr, ram_if_data,
		ram_mem_rw, ram_mem_addr, ram_mem_rdata, ram_mem_wdata
	);

	// 全局状态
	// 当前测试的名称
	reg [256*8:1] testname;

	// 辅助函数
	task tick();
	begin
		#1 clk = 1;
		#1 clk = 0;
	end
	endtask
	task tickn(input integer n);
		integer i;
	begin
		for (i = 0; i < n; i=i+1)
			tick();
	end
	endtask
	
	task reset();
		integer i;
	begin
		rst = 1'b1;
		tick();
		rst = 1'b0;
		
		for (i=0; i<128*1024; i=i+1)
			ram[i] = 0;
	end
	endtask

	task load(input reg [256*8:1] hexfile);
	begin
		$readmemh({`TESTBENCH_PWD, "/build/", hexfile}, ram);
	end
	endtask

	`define assert(signal, value) \
        if (signal !== value) begin \
            $display("ASSERTION FAILED in %m: signal != value"); \
            $stop; \
        end

	
	// 测试集
	initial
	begin : TB

		// 记录 core 模块以及其下 1 层子模块的所有变量波形
		$dumpfile("coredump.vcd");
		$dumpvars(1, core);
		$dumpvars(0, core.gprs.regs);
	
		reset();
		load("add.hex");
		tickn(12);
		`assert(core.gprs.regs[2], 144)
		`assert(core.gprs.regs[3], 102)

		
		reset();
		load("fib10.hex");
		tickn(35);
		`assert(core.gprs.regs[2], 89)
		

		reset();
		load("arich.hex");
		tickn(16);
		`assert(core.gprs.regs[5], 8)
		`assert(core.gprs.regs[6], 4)
		`assert(core.gprs.regs[7], 16)


		reset();
		load("ldst.hex");
		tickn(11);
		`assert(core.gprs.regs[2], 32'h12345678)
		`assert(core.gprs.regs[3], 32'h12345678)
		`assert(core.gprs.regs[4], 32'h12345678)

		
		$dumpflush;
		$display("All test finished");
	end
endmodule
